%import common.CNAME -> IDENTIFIER
%import common.ESCAPED_STRING -> STR
%import common.SIGNED_NUMBER -> NUMBER
%import common.WS
%ignore WS

SINGLE_QUOTED_STRING : /'[^']*'/
DATE : /[0-9]{4}-[0-9]{2}-[0-9]{2}/
table_name : IDENTIFIER
column_name : IDENTIFIER

LP : "("
RP : ")"
LESSTHAN : "<"
LESSEQUAL : "<="
GREATERTHAN: ">"
GREATEREQUAL : ">="
EQUAL: "="
NOTEQUAL: "!="

SELECT : "select"i
FROM : "from"i
WHERE : "where"i
AS : "as"i
IS : "is"i
OR : "or"i
AND : "and"i
NOT : "not"i
NULL : "null"i

start : select_query ";"
select_query : SELECT select_list table_expression
select_list : "*" | selected_column ("," selected_column)*
selected_column : [table_name "."] column_name [AS column_name]
table_expression : from_clause [where_clause]
from_clause : FROM table_reference_list
table_reference_list : referred_table ("," referred_table)*
referred_table : table_name [AS? table_name]
where_clause : WHERE boolean_expr

boolean_expr : boolean_term (OR boolean_term)*
boolean_term : boolean_factor (AND boolean_factor)*
boolean_factor : [NOT] boolean_test
boolean_test : predicate | parenthesized_boolean_expr
parenthesized_boolean_expr : LP boolean_expr RP

predicate : comparison_predicate | null_predicate
comparison_predicate : comp_operand comp_op comp_operand
comp_op: LESSTHAN | LESSEQUAL | EQUAL | GREATERTHAN | GREATEREQUAL | NOTEQUAL
comp_operand : comparable_value | [table_name "."] column_name
comparable_value : NUMBER | STR | DATE | SINGLE_QUOTED_STRING 

null_predicate : [table_name "."] column_name null_operation
null_operation : IS [NOT] NULL

